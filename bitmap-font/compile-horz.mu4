( This file is part of muFORTH: http://muforth.nimblemachines.com/

  Copyright 2002-2010 David Frech. All rights reserved, and all wrongs
  reversed. (See the file COPYRIGHT for details.)


loading Horizontal scan bitmap compiler


( For console fonts, for use with "pin-head" impact printers, and for
  spelling in falling water, we need to scan the character images
  horizontally. It's easy to do this. We assume here that our characters
  are no more than 8 bits wide, and - mostly for compatibility with "raw"
  font files - at most 16 pixels high.)

16 constant height  ( XXX really, "caller" should set this)

: .   ( a bits - a bits')   2* ;               ( unset pixel)
: X   ( a bits - a bits')   .  1+ ;            ( set pixel)
: |   ( a bits - a+1 0)     over c!  1+  0 ;   ( end of raster line)
: |;  ( a bits)             |  2drop ;         ( end of character)

( How many alternate shapes?)
4 constant #alts

height #alts *  constant b/char

( Assuming 128 chars, less controls and space)
128 33 - b/char *  buffer bitmaps

: bitmap  ( alt# - a bits)
   height * ( alt offset)
   \f char  33 - ( skip controls & space)  b/char *  bitmaps +  ( char origin)
   + ( alt offset)  0 ( bits) ;

: letter   ( - a bits)  0 bitmap ;
: alt1     ( - a bits)  1 bitmap ;
: alt2     ( - a bits)  2 bitmap ;
: alt3     ( - a bits)  3 bitmap ;
: rounder  ( - a bits)  alt1 ;  ( rounder versions are simply alt 1)

( XXX currently no way to mark descenders, and no way to create "icons"
  that are a different shape than the standard character box.)

