( This file is part of muFORTH: http://pages.nimblemachines.com/muforth

  Copyright 1997-2008 David Frech. All rights reserved, and all wrongs
  reversed. (See the file COPYRIGHT for details.)

forth

( Generic control structures. This file is loaded my the main startup file
  - startup.mu4 - and can be loaded by metacompilers as well.)

// Architecture-specific code defines the following:
//
//   resolve>     ( src dest)
//   mark>        ( - src)
//   branch,
//   0branch,
//   =0branch,
//   for,
//   ?for,
//   next,
//   \            compile from "compiler" or "inside" chain

: <resolve  (s dest src)  swap resolve> ;   ( generic)

compiler
: =if   (s - src)  =0branch, mark> ;
: if    (s - src)   0branch, mark> ;
: then  (s src)  here resolve> ;
// : else      (s src0 - src1)  branch, mark>  swap  \ then ;

: begin   (s - dest)  here ;
: =until  (s dest -)       \ =if  <resolve ;
: until   (s dest -)        \ if  <resolve ;
: again   (s dest -)    branch, mark>  <resolve ;
: =while  (s dest - src dest)  \ =if  swap ;
: while   (s dest - src dest)   \ if  swap ;
: repeat  (s src dest -)   \ again  \ then ;

( n for .. next         goes n times; 2^wordsize if n=0 )
( n ?for .. next then   goes n times; 0 if n=0 )

: for     (s - dest)      for,         \ begin ;
: ?for    (s - src dest)  ?for, mark>  \ begin ;
: next    (s dest -)      next, mark>  <resolve ;

forth
