( This file is part of muFORTH: http://muforth.nimblemachines.com/

  Copyright 2002-2010 David Frech. All rights reserved, and all wrongs
  reversed. (See the file COPYRIGHT for details.)

( Simple support to print out contents of a FAT filesystem's boot block,
  which defines various parameters, including the total size, the cluster
  size, and the geometry.)

: skip  ( a skip -- a+skip a)  over +  swap ;
: b   1 skip  c@ ;
: w   b push  b 8 <<  pop + ;  ( little-endian word fetch)
: l   4 skip   @ ;

: info  ( block)
   3 +  ( skip jump instruction)  cr
   8 skip  8  cr type  ( OEM signature)
   w  cr  u. ." bytes per sector"
   b  cr  u. ." sectors per cluster"
   w  cr  u. ." reserved sectors"
   b  cr  u. ." FAT table(s)"
   w  cr  u. ." root directory entries"
   w  cr  u. ." total sectors (short)"
   b  cr  hex  u.  decimal  ." media descriptor"
   w  cr  u. ." sectors per FAT (short)"
   w  cr  u. ." sectors per track"
   w  cr  u. ." heads"
   l  cr  u. ." hidden sectors"
   l  cr  u. ." total sectors (long)"
   l  cr  u. ." sectors per FAT (long)"   
   cr  drop ;

( Open device or copied block.)
-- z" /dev/sdb1" 
-- z" dos-info-block"

: doit  ( parse filename, open file, print info, close)
   token, ( consume filename from command line)
   dup count type
   open-file-ro  dup  read-file  ( fd a len)  drop
   info  close-file  bye ;

