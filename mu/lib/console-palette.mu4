( This file is part of muFORTH: http://muforth.nimblemachines.com/

  Copyright 2002-2015 David Frech. (Read the LICENSE for details.)

( Fiddle around with the console colour palette, on Linux.)

48 buffer palette  ( 16 colours, one byte each of R, G, B)

: get-palette   0 "4b70 palette ioctl ;
: set-palette   0 "4b71 palette ioctl ;

: >pal  ( n - offset)  3 *  palette + ;
: pal!  ( r g b n)    >pal push  r@ 2 + c!  r@ 1+ c!  pop c! ;
: pal@  ( n - r g b)  >pal push  r@ c@  r@ 1+ c@  pop 2 + c@ ;

( from colorforth code:)
( terminal ESC sequences)
: <esc  <#  char m hold ;
: esc>  char [ hold  ctrl [ hold  0  #> type ;

: attrib   #s drop ;
: attrib;  attrib  char ; hold ;

( NOTE: 39 sets default fg; 49 default bg)
: color  ( bright foregd backgd)
   radix preserve  decimal
   <esc  40 + attrib;  30 + attrib;  attrib  esc> ;

( Sequence of each subset of 8 colors:
: black    0 ;
: red      1 ;
: green    2 ;
: yellow   3 ;
: blue     4 ;
: magenta  5 ;
: cyan     6 ;
: white    7 ;
)

( Show all colors)
: set-color  ( n)  8 /mod swap  9  color ;
: .color  ( n)  dup set-color  7 and  char 0 + dup emit emit ;
: cc  16 0 do  i .color  loop  0 9 9 color  ( back to defaults) ;
