( This file is part of muFORTH: http://muforth.nimblemachines.com/

  Copyright 2002-2014 David Frech. All rights reserved, and all wrongs
  reversed. (See the file COPYRIGHT for details.)

( Find integer ratios that closely approximate useful real number constants.)

variable target  ( the value we are trying to approximate)
variable found   ( ugly way to exit do loop)

: try  ( num den)
   =if  ( throw out zero denominators!)
      2dup  1,000,000,000 swap */  target @  -
      dup 0= if found on then
      dup 500 +  1,000 u< if  cr  rot . swap .  .  ^  then
      drop
   then  2drop ;

( Variables for calculating the useful range of denominators to try)
variable low
variable hi

( approx spews out a series of numerator-denominator pairs, followed by the
  delta between the desired value and the approximation. It stops when the
  delta is zero - which means that the error of the rational approximation is
  less than 1e-9!)

: approx   dup target !  1,000,000 /  dup hi !  1+ low !  found off
   1,000,000 1 do
      i 1,000 hi @ */ 1+  i 1,000 low @ */  do  j i  try  loop
      -- i 1,000 hi @ */ 1+  i 1,000 low @ */  do  cr  j . i .  loop
   found @ ?leave  loop ;

: pi   3,141,592,654  >stdout  approx ;
: e    2,718,281,828  >stdout  approx ;
