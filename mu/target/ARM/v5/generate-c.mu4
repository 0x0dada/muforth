( This file is part of muFORTH: http://muforth.nimblemachines.com/

  Copyright 2002-2014 David Frech. All rights reserved, and all wrongs
  reversed. (See the file COPYRIGHT for details.)


loading Generation of C image

( Code to generate C code that "represents" the contents of the
  meta-compiled image, and also the relocation bitmap.)

forth decimal

: .n  <# # # # # # # # #  char x hold  char 0 hold #>  type  ;

: .w  .n char , emit  space ;

: .a  ." /* " cells .n ." : */  "  ;

: .words  ( a n)  0 -rot  for
   over 3 and 0= if cr over .a then  1 u+
   @+  swap  .w   next  2drop ;

: gen-c-image
   hex  >stdout
   cr ." int image[] = {"
      image  \m here cell/  .words
   cr ." };"

   cr
   cr ." int reloc_bitmap[] = {"
      0 reloc-bitmap  \m here cell/ 31 + 5 >>  tuck  ( ncells 'reloc ncells)
      .words
   cr ." };"
   cr

   decimal
   cr ." int image_sz = " \m here .  ." ;"
   cr ." int reloc_ncells = " .  ." ;"
   cr
;
