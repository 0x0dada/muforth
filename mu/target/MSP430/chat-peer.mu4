( This file is part of muFORTH: http://muforth.nimblemachines.com/

  Copyright 2002-2015 David Frech. (Read the LICENSE for details.)

loading MSP430 serial chat protocol (peer)

( NOTE: This might seem a bit odd, but this file implements the side of the
  chat protocol normally implemented by firmware in a target chip! The
  reason for doing this here, in muforth, is to test out the protocol by
  having both sides running on the host.)

hex

( Taking inspiration from the wildly successful HC08 serial chat protocol.

  Responds to the following commands. NOTE: these are hex values!

00 - 0f  Idle   - these command bytes are ignored

10  GetVersion  - get the chat protocol version
11  SetAddr     - set the memory address pointer
12  GetSP       - get stack pointer - points to saved regs
13  ReadWord    - read a word from memory, incr pointer
14  ReadWords   - read N words, incrementing as we go
15  WriteWord   - write a word to memory, incr pointer
16  Run         - set the pc in the saved frame, pop the registers and
                - saved status, and go
17  FlashWord   - write word to flash, incr pointer
                - execute command saved on chat frame
                - target returns flash status
18  GetCheck    - get current checksum

19 - ff  Idle   - these command bytes are ignored
)

variable pty-master
: pty-send  ( b)     pty-master @ >emit ;
: pty-recv  ( - b)   pty-master @ <key ;

( Spying on the protocol.)
variable spy  spy on
: send             spy @ if ." >"  dup .h8_ then  pty-send ;
: recv   pty-recv  spy @ if ." <"  dup .h8_ then ;

variable checksum
: sum!  0ffff checksum ! ;
: sum  ( w)  checksum @  xor  checksum ! ;

: >b   send ;
: b>   recv ;
: >w   dup sum  >hilo  >b >b ;
: w>   b> b>  lohi>  dup sum ;

variable peer-sp
: p.push  ( w)     peer-sp @       \m cell-  dup  peer-sp !  image-! ;
: p.pop   ( - w)   peer-sp @  dup  \m cell+       peer-sp !  image-@ ;

: peer-push-frame
   @ram #ram +  peer-sp !
   ff4c p.push  ( outer PC)
   fefe p.push  ( frame PC)
   0 p.push ( SR)
   4008 p.push ( Y)  e665 p.push ( X)  cafe p.push ( TOP)
   0200 p.push ( Count)  0308 p.push ( RP)  e844 p.push ( W)  c29e p.push ( IP)
   0 p.push ( FCMD) ;

( Compile the first 32 bits of the current muforth Git commit.
  When asked for the version, return these two 16-bit words, in
  little-endian order.)

: p.GetVersion   [ muforth-commit drop 8 evaluate ]  dup >w  10 >> >w ;

: p.SetAddr      w>  image+  m ! ;
: p.GetSP        peer-sp @ >w ;

: p.ReadWord     m* m* lohi> >w ;
: p.WriteWord    w> >hilo m& m& ;

: p.ReadWords    b>  dup sum  ?for  p.ReadWord  next  then ;

: p.Run
   "c0de checksum !   ( emulate code that clobbers Y when run)
   peer-push-frame ;

: peer-erase-page
   m @  image-  ( 'target)  [ /page 1- invert ] and  image+  m !
   /page for  0ff m&  next  [ /page negate ]  m +! ;

( Only do the flash operation if FCMD is valid.)
: p.FlashWord
   p.pop  dup p.push  ( get FCMD from stack)
   dup  [ \eq FWRT    \eq FKEY  + ] = if  drop  p.WriteWord  9658 >w  ^  then
   w> drop
        [ \eq FERASE  \eq FKEY  + ] = if  peer-erase-page  9658 >w  ^  then
   f00d >w  ( bad flash command) ;

: p.GetCheck     checksum @ >w  sum! ;

: peer-command
   b>  dup sum  10 - dup 9 u< if  jump
      p.GetVersion  p.SetAddr  p.GetSP
      p.ReadWord  p.ReadWords  p.WriteWord
      p.Run  p.FlashWord  p.GetCheck
   then  drop ;

: re-peer
   open-pty  cr  ." Connect to "  zcount type  ."  to chat with this peer."
   pty-master !  begin  peer-command  again ;

: peer
   peer-push-frame  begin  catch re-peer  pty-master @ close-file  again ;

( Let's run it!)
peer
