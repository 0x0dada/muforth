( This file is part of muFORTH: http://muforth.nimblemachines.com/

  Copyright 2002-2010 David Frech. All rights reserved, and all wrongs
  reversed. (See the file COPYRIGHT for details.)

cr " ARM memory allocation "  file[#

( A single memory space.)
256 Ki constant #image
#image buffer image

forth
: image+  image  +  ;
: image!  image  +  ! ;  ( NOTE: ! in this context assumes host cell size == target cell size + endianness! )
: imagec! image  +  c! ;

meta
: cells  2 << ;  ( target cells are 32 bits)
: cell/  2 >> ;  ( signed)
1 \m cells constant cell
: cell+  \m cell + ;
: cell-  \m cell - ;
forth

( We keep of bitmap of which cells need relocation.)
#image \m cell/ ( num of bits in bitmap)
   5 >> ( num of cells in bitmap)
   constant #reloc-bitmap

#reloc-bitmap array reloc-bitmap

( Mark a target address as needing relocation.)
: index>bit  ( index - cell-offset mask)  dup 5 >>  swap 31 and 1 swap << ;

: 'reloc  ( target-addr - mask a)
   \m cell/  ( convert to cell index)
   index>bit  swap  reloc-bitmap ( mask a) ;

: reloc  ( a)
   'reloc  dup  @  rot  or  swap  ! ;

( Basic dictionary words.)
meta
variable h  ( dictionary pointer)
: here  \m h  @ ;
: aligned  [ \m cell 1 - ] + [ \m cell negate ] and ;
: align   \m here  \m aligned  \m h ! ;
: allot   \m h +! ;
: ,    \m here  image!  \m cell \m allot ;
: a,   ( adresses that needs relocation)  \m here  reloc  \m , ;
: c,   \m here  imagec!  1  \m allot ;
forth
