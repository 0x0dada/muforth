( This file is part of muFORTH: http://muforth.nimblemachines.com/

  Copyright 2002-2012 David Frech. All rights reserved, and all wrongs
  reversed. (See the file COPYRIGHT for details.)

loading ARM Cortex-M interaction

variable chatting

( XXX Don't define chat words unless there is support in a connected
  device.)

.ifdef t.chat

: +chat  chatting on ;

: read-cell  ( cell or h)   |cell @  4 = if
   t.readw  ( read 32-bit word from target)  ^  then
   t.readh  ( read 16-bit word from target) ;

: >chat  ['] t.setaddr  is |p!
         ['] t.getaddr  is |p@
         ['] t.readb    is |c*
         ['] read-cell  is |cell*
                         4 |addr !  ( size of addr) ;

: chat  +chat  >chat  t.chat ;

.else  ( NO CONNECTED CHAT TARGET!)

: chat  error" No connected chat-capable target." ;
: >chat ;

.then


( Define our own key bindings for memory dumping and disassembly. We'll
  default to host bindings if there isn't one in our array. This way we
  only have to define the "delta" between host and target behaviours.)

128 array cortex-seekeys

( Default key action is to run host key code)
host-seekeys cortex-seekeys  128 cells cmove

cortex-seekeys 'seekeys !    ( switch over to our bindings)

( Support for dumping memory)
: 1dump  ( a)
   hex-bytes
   chatting @ if   >chat  else  >image  then
   dup _addr  dup .chars
   dup .addr  dup .hex-bytes
   dup _addr  dup .hex-cells
   drop ;

: dumping
   ['] skip+  advance!
   ['] skip-  retreat!
   ['] 1dump  inspect! ;

: disasming
   ['] i-skip  advance!
   ['] 0       retreat!
   ['] 1dis    inspect! ;

( Re-masks address and re-aligns it to |cell size. Useful when switching
  modes or between halfword and fullword display.)

: see-align  ( a - a 0)  0 advance ;

key: d  ( a - a 0)   dumping    see-align ;
key: i  ( a - a 0)   disasming  see-align ;

key: h  ( a - a 0)   2 |cell !  see-align ;
key: w  ( a - a 0)   4 |cell !  see-align ;

: >target
   cortex-seekeys 'seekeys !    ( switch over to our bindings)
   4  |cell @  u< if  ( still set for host - reset it)
      4 |cell !  ( default to "big" cells)  then
   ;

( Interactive)
( make an alias so we can still get to it)
: du-host  >host  dumping  du ;

: du  ( a - a')
   >target  dumping  inspect ;

: dis  ( a - a')
   >target  disasming  inspect ;
