( This file is part of muFORTH: http://muforth.nimblemachines.com/

  Copyright 2002-2009 David Frech. All rights reserved, and all wrongs
  reversed. (See the file COPYRIGHT for details.)

( I couldn't take it any more - the 908 ROM monitor was driving me crazy.
  It's slow, and there is still an odd bug with the PC - the "image" of it on
  the stack frame - not getting properly initialised... but only when the
  target is first connected!

  I decided to try out some simple ideas. Instead of the Nibble Machines
  loader, we have this! Rather than a Bootloader, it's a Byteloader?)

cr " HC908 Chat (host) "  file[#

decimal

( Spying on the protocol.)
variable spy  spy off

: send          spy @ if ." >"  dup .h8_ then  _send ;
: recv   _recv  spy @ if ." <"  dup .h8_ then ;

( Host side)
: c.Run        00 send  recv ( flags)  drop ;  ( wait for response)
: c.Go         00 send  ;                      ( don't wait)
: c.Id         01 send  recv ;
: c.SetH       02 send  send ;
: c.SetX       03 send  send ;
: c.GetH       04 send  recv ;
: c.GetX       05 send  recv ;
: c.HXtoSP     06 send  ;
: c.SPtoHX     07 send  ;
: c.ReadByte   08 send  recv ;
: c.WriteByte  09 send  send ;
: c.Idle       10 send  ;

( Resynchronise the protocol - just to make sure. We throw away any unread
  input, and then send one ChatIdle, just in case we were in the middle of
  a SetH/SetX/WriteByte. This puts us back into a known state.)

: resync  flush  c.Idle ;

( We resync right before reading from a new memory address, and when
  getting the SP. We don't do these all the time, but when we do, we'd like
  to get the right addresses! By "sprinkling" these protocol resets
  throughtout the interaction, it's difficult for the two machines to stay
  out of whack for very long. Doing a "du" or ".regs" usually resets
  things.)

: c.SetHX  resync  >lohi  c.SetH  c.SetX ;
: c.GetHX                 c.GetH  c.GetX  hilo> ;
: c.GetSP  resync  c.SPtoHX  c.GetHX ;
: c.SetSP                    c.SetHX  c.HXtoSP ;

: setup-hx  ( a)  dup match-hx?  over 1 update-hx  if drop ^ then  c.SetHX ;
: _chat-c@  ( a - byte)  setup-hx  c.ReadByte ;
: _chat-c!  ( byte a)    setup-hx  c.WriteByte ;

: chat
   tty-target-init  115200 bps
   ( Wire our versions into the interact code.)
   ['] c.Run is chat-run     
   ['] c.GetSP is chat-get-sp  
   ['] _chat-c@ is chat-c@
   ['] _chat-c! is chat-c!
   ['] _chat-c@ is |c@
   c.GetSP chat-sp !  0 c.SetHX  chat-hx off ( so they match!) ;

   ( XXX - call hi? Only if it's modified to _not_ copy code over when it's
     compiled for flash!)

#]file

