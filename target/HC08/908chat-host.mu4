( This file is part of muFORTH: http://muforth.nimblemachines.com/

  Copyright 2002-2009 David Frech. All rights reserved, and all wrongs
  reversed. (See the file COPYRIGHT for details.)

( I couldn't take it any more - the 908 ROM monitor was driving me crazy.
  It's slow, and there is still an odd bug with the PC - the "image" of it on
  the stack frame - not getting properly initialised... but only when the
  target is first connected!

  I decided to try out some simple ideas. Instead of the Nibble Machines
  loader, we have this! Rather than a Bootloader, it's a Byteloader?)

cr " HC908 Chat (host) "  file[#

decimal

( Spying on the protocol.)
variable spy  spy off

: send          spy @ if ." >"  dup .h8_ then  _send ;
: recv   _recv  spy @ if ." <"  dup .h8_ then ;

( Host side)
: Run        00 send  recv ( flags)  drop ;
: SetH       01 send  send  ;
: SetX       02 send  send  ;
: GetH       03 send  recv ;
: GetX       04 send  recv ;
: HXtoSP     05 send  ;
: SPtoHX     06 send  ;
: ReadByte   07 send  recv ;
: WriteByte  08 send  send  ;
: Sync       09 send  ;

( Resynchronise the protocol - just to make sure. We throw away any unread
  input, and then send one Sync, just in case we were in the middle of
  a SetH/SetX/WriteByte. This puts us back into a known state.)

: resync  flush  Sync ;

( We resync right before reading from a new memory address, and when
  getting the SP. We don't do these all the time, but when we do, we'd like
  to get the right addresses! By "sprinkling" these protocol resets
  throughtout the interaction, it's difficult for the two machines to stay
  out of whack for very long. Doing a "du" or ".regs" usually resets
  things.)

: SetHX  resync  >lohi  SetH  SetX ;
: GetHX                 GetH  GetX  hilo> ;
: GetSP  resync  SPtoHX  GetHX ;
: SetSP   SetHX  HXtoSP ;

: setup-hx  ( a)  dup match-hx?  over 1 update-hx  if drop ^ then  SetHX ;
: _chat-c@  ( a - byte)  setup-hx  ReadByte ;
: _chat-c!  ( byte a)    setup-hx  WriteByte ;

: chat
   tty-target-init  115200 bps
   ( Wire our versions into the interact code.)
   ['] Run is chat-run     
   ['] GetSP is chat-get-sp  
   ['] _chat-c@ is chat-c@
   ['] _chat-c! is chat-c!
   ['] _chat-c@ is |c@
   GetSP chat-sp !  0 SetHX  chat-hx off ( so they match!) ;

   ( XXX - call hi? Only if it's modified to _not_ copy code over when it's
     compiled for flash!)

#]file

