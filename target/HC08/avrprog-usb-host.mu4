( This file is part of muFORTH: http://muforth.nimblemachines.com/

  Copyright 2002-2012 David Frech. All rights reserved, and all wrongs
  reversed. (See the file COPYRIGHT for details.)

loading AVR SPI-over-USB programmer (host)

( Host-side code to drive the AVR programmer code in
  target/HC08/avrprog-usb-core.mu4.)

hex

-- : usb  ( bmRequest bRequest wValue wIndex wLength 'buffer)

( Since we're using wValue and wIndex to pass an array of four bytes, we
  need an easy way to assemble the two word values -- little-endian! --
  from two bytes.)

: >setup   ( b0 b1 b2 b3 - wValue wIndex)
   lohi> push  lohi> pop ;

( Commands)
: avr.Result   0c0 20   0 0     4 pad  usb ;

: avr.Execute   ( b0 b1 b2 b3)  >setup 2push
                40 21   2pop    4 pad  usb ;

: control-write
   create ,  ( bRequest)
   does> @   40 swap  0 0 0 0  usb ;

00  control-write  avr.Bye  ( End session, return to chat command loop)
22  control-write  avr.ResetLow
23  control-write  avr.ResetHigh
24  control-write  avr.SlowClock  ( 250k)
25  control-write  avr.FastClock  ( 2M)

: avr   ( b0 b1 b2 b3)  avr.Execute  avr.Result ;

: avr2  ( b0 b1 b2 b3 - r2)  ( do command; return second-to-last byte of response)
   avr  pad 2 + c@ ;

: avr3  ( b0 b1 b2 b3 - r3)  ( do command; return last byte of response)
   avr  pad 3 + c@ ;

: prog  ( enable programming)
.ifdef in-ram
   0112 u.SetPC u.Run  ( run from ram - loads right after flash routine)
.else
   0fa00 u.SetPC u.Run
.then
   10 for  avr.ResetLow  0ac 53 0 0 avr2  53 = if  pop drop ^ then
           avr.ResetHigh  next
   ." Couldn't enable serial programming" ;

: unprog  ( disable programming)
   avr.ResetHigh  avr.Bye ;

: sig  ( - s0 s1 s2)
   30 0 0 0 avr3
   30 0 1 0 avr3
   30 0 2 0 avr3 ;

: fuses  ( - fuse hfuse efuse)
   50 0 0 0 avr3
   58 8 0 0 avr3
   50 8 0 0 avr3 ;

: lock  ( - lock)
   58 0 0 0 avr3 ;
