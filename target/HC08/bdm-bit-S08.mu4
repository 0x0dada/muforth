( This file is part of muFORTH: http://muforth.nimblemachines.com/

  Copyright 2002-2012 David Frech. All rights reserved, and all wrongs
  reversed. (See the file COPYRIGHT for details.)

loading HCS08 BDM support (bit routines for S08)

( Support for Freescale's Background Debug Mode - or BDM.)

( These are bit send and receive commands for an S08 processor running at
  18.432M - bus speed of 9.216M - and talking to S08 targets running at
  4M and 8M.

  These routines are a bit different from their cousins for the 908 for
  several reasons:

  * the clock speeds are different - the S08 is running twice as fast as
    its 908 counterpart;

  * the instruction timings are different - not only how many cycles an
    instruction takes, but the positioning and read and write cycles is
    quite different between the 908 and S08;

  * I don't have the same 5v-to-3v translation issues, so driving the BKGD
    pin on another S08 _from_ an S08 - both at 3v - is easy.)

PTAD  equ PortA
PTADD equ DirA

( We are using PortA0 [TPMCH0] for everything.

  To transmit, we set DirA0 high for the duration of the _byte_, and simply
  toggle the data value on PA0.

  To receive, we set PA0 low for the duration of the byte, and toggle the
  DirA0 to either drive a start bit, or listen for the target's response.)

code BdmDriveLow
   PortA 0 bclr
   DirA 0 bset
   rts  ;c

( Called when BDM loop starts up; return to canonical state.)
code BdmHiZ
   TPMC0SC ) clr  ( take pin from timer
   PortA 0 bset   ( before tri-stating, briefly drive high)
   DirA 0 bset
   DirA 0 bclr    ( tri-state A0)
   rts  ;c


( The magic numbers for bit-banging BDM are as follows:

  * Start bit should be 4 target cycles long.

  * Received bits should be sampled on the 10th target cycle, counting from
    the falling edge of the start bit.

  * Transmitted bits should be held for 13 target cycles, counting from
    the falling edge of the start bit.
)


( BDM code for host S08 running at 9.216M, and target at 4M.

  4M target       9.216 host
  =========       ==========
     4               9.2
    10              23
    13              30
)

code SetTx1_4M
   MakeJmp bsr  ;c   -- pushes address of following routine on stack!

( Send one bit via BDM)
code tx1_4m  ( tx bit in A0)
   ( 9~ start bit; hold for 30~)
   PortA 0 bclr              ( rfwpp  - drive start bit)
   0 ,x tst                  ( rfp    - delay 6 cycles)
   0 ,x tst                  ( rfp    )
   PortA ) sta               ( wpp    - drive data bit)
   0 ,x tst                  ( rfp    - delay 16 cycles)
   0 ,x tst                  ( rfp    )
   0 ,x tst                  ( rfp    )
   0 ,x tst                  ( rfp    )
   0 ,x tst                  ( rfp    )
   nop                       ( p      )
   PortA 0 bset              ( rfwpp  - drive stop bit)
   rts  ;c

code SetRx1_4M
   MakeJmp bsr  ;c   -- pushes address of following routine on stack!

( Recv one bit via BDM)
code rx1_4m  ( recvd bit in A0)
   ( 9~ start bit; sample at 23~)
   DirA 0 bset               ( rfwpp  - drive start bit)
   0 ,x tst  nop             ( rfp.p  - delay 4 cycles)
   DirA 0 bclr               ( rfwpp  - tri-state BKGD)
   0 ,x tst                  ( rfw    - delay 11 cycles)
   0 ,x tst                  ( rfw    )
   .a psh  .a pul            ( sp.ufp - delay 5 cycles)
   PortA ) lda               ( rpp    - sample bit)
   rts  ;c


( BDM code for host S08 running at 9.216M, and target at 8M.

  8M target       9.216 host
  =========       ==========
     4               4.6
    10              11.5
    13              15
)

code SetTx1_8M
   MakeJmp bsr  ;c   -- pushes address of following routine on stack!

( Send one bit via BDM)
code tx1_8m  ( tx bit in A0)
   ( 5~ start bit; hold for 15~)
   PortA 0 bclr              ( rfwpp  - drive start bit)
   nop nop                   ( p.p    - delay 2 cycles)
   PortA ) sta               ( wpp    - drive data bit)
   .a psh  .a pul            ( sp.ufp - delay 5 cycles)
   PortA 0 bset              ( rfwpp  - drive stop bit)
   rts  ;c

code SetRx1_8M
   MakeJmp bsr  ;c   -- pushes address of following routine on stack!

( Recv one bit via BDM)
code rx1_8m  ( recvd bit in A0)
   ( 5~ start bit; sample at 12~)
   DirA 0 bset               ( rfwpp  - drive start bit)
   DirA 0 bclr               ( rfwpp  - tri-state BKGD)
   0 ,x tst  nop             ( rfp.p  - delay 4 cycles)
   PortA ) lda               ( rpp    - sample bit)
   rts  ;c

( BDM code for host S08 running at 9.216M, and target at 16M.

  16M target      9.216 host
  =========       ==========
     4               2.3
    10               5.7
    13               7.4
)

code SetTx1_16M
   MakeJmp bsr  ;c   -- pushes address of following routine on stack!

( Send one bit via BDM)
code tx1_16m  ( tx bit in A0)
   ( 3~ start bit; hold for 8~)
   PortA 0 bclr              ( rfwpp  - drive start bit)
   PortA ) sta               ( wpp    - drive data bit)
   PortA 0 bset              ( rfwpp  - drive stop bit)
   rts  ;c

code SetRx1_16M
   MakeJmp bsr  ;c   -- pushes address of following routine on stack!

( Recv one bit via BDM)
code rx1_16m  ( recvd bit in A0)
   ( 3~ start bit; sample at 6~)
   .a clr
   -- DirA ) lda   "0fe # and   ( get ready to clear low bit)
   DirA 0 bset               ( rfwpp  - drive start bit)
   DirA ) sta                ( wpp    - tri-state BKGD)
   PortA ) lda               ( rpp    - sample bit)
   rts  ;c

( Send one byte via BDM.)
( NOT the usual X stack discipline with two slots free!)
code BdmTx  ( tx byte at 0,x)
   ( loop 8; generate bits MSB first)
   8 # lda   .a psh
   PortA 0 bset ( idle A0 high)
   DirA 0 bset  ( drive A0 for the duration of the byte)
   begin
      0 ,x rol  .a rol  ( bit into A0)
      BdmTx1 ) jsr
   0 ,s decz?  until
   DirA 0 bclr  ( done xmitting; tri-state A0)
   .a pul
   1 # aix   rts  ;c

( Recv one byte via BDM.)
( NOT the usual X stack discipline with two slots free!)
( BdmRx1 leaves received bit in C.)
code BdmRx  ( recvd byte to 0,x)
   ( loop 8; recv bits MSB first; bit is 01)
   8 # lda   .a psh   0 ,x clr
   PortA 0 bclr ( start bit is zero)
   DirA 0 bclr  ( make sure A0 tri-stated)
   begin
      BdmRx1 ) jsr  .a ror  ( A0 -> C)  0 ,x rol
   0 ,s decz?  until
   .a pul
   1 # aix   rts  ;c

code SyncWait  -- clobbers A
   .a clr
   begin
      TPMC0SC 7 bclr? while ( edge not triggered)
      .a psh  .x psh  mul  .x pul  .a pul  ( 15~)
   .a decz?  until  then  rts  ;c

code SyncPulse
   08 # TPMSC ) mov     -- start timer; clock from bus clock
   BdmHiZ ) jsr         -- start in a known state
   BdmDriveLow ) jsr
   SyncWait bsr         -- drive BKGD low for a long time
   BdmHiZ ) jsr         -- drive high briefly before tristating
   08 # TPMC0SC ) mov   -- capture falling edge on BKGD in CH0 (PA0)

   -- now target waits 16 BDM cycles, then drives BKGD low for 128
   SyncWait bsr
   TPMC0SC 7 bset? if  ( falling edge triggered)
      ( on S08 can read capture registers in either order)
      TPMC0VL ) lda   1 ,x sta
      TPMC0VH ) lda   0 ,x sta
      04 # TPMC0SC ) mov   -- capture rising edge on BKGD in CH0 (PA0)
      SyncWait bsr
      TPMC0SC 7 bset? if  ( rising edge triggered)
         ( If both falling and rising triggered, calculate difference;
           otherwise return 0.)
         TPMC0VL ) lda   1 ,x sub   1 ,x sta
         TPMC0VH ) lda   0 ,x sbc   0 ,x sta
         TPMC0SC ) clr        -- take pin from timer
         rts
      then
   then

   ( return 0)
   1 ,x clr  0 ,x clr
   TPMC0SC ) clr        -- take pin from timer
   rts  ;c

