( This file is part of muFORTH: http://muforth.nimblemachines.com/

  Copyright 2002-2012 David Frech. All rights reserved, and all wrongs
  reversed. (See the file COPYRIGHT for details.)

loading HCS08 BDM-over-USB transport (host)

( Host-side - ie PC - code to drive the BDM-over-USB implementation in
  target/HC08/bdm2-usb.mu4.)

( NOTE: Before loading this file, load an S08 variant device file. This
  code requires several of the register defines, and needs to know where to
  find the clock trim register.)

hex

-- : usb  ( bmRequest bRequest wValue wIndex wLength 'buffer)
.ifdef test-usb
: usb  ( bmRequest bRequest wValue wIndex wLength 'buffer)
   cr  6 for  u.  next ;
.then

( Since we're using wValue and wIndex to pass an array of four bytes, we
  need an easy way to assemble the two word values -- little-endian! --
  from two bytes.)

: >setup   ( cmd repeat size expect - wValue wIndex)
   lohi> push  lohi> pop ;

( Commands)
: u.Result  ( #expect)  push  ( fetch BDM result)
   0c0 20  0 0   pop  pad  usb ;

: u.BdmRead    ( cmd repeat size buffer)
   push ( buffer)
   2dup * push ( len)  ( len = repeat * size)
   0 ( expect)  >setup 2push
   0c0 21  2pop  pop pop  usb ;

: u.BdmWrite   ( cmd repeat size expect buffer)
   push ( buffer)
   -rot  2dup * push ( len) rot  ( len = repeat * size)
    >setup 2push
    40 22  2pop  pop pop  usb ;

: u.Sync    0c0 23  0  0  2 pad  usb ;

: control-write
   create ,  ( bRequest)
   does> @   40 swap  0 0 0 0  usb ;

00  control-write  b.Bye  ( End session, return to chat command loop)
24  control-write  b.BkgdLow
25  control-write  b.BkgdHiZ
26  control-write  b.ResetLow
27  control-write  b.ResetHiZ
28  control-write  b.Clock4M
29  control-write  b.Clock8M
2a  control-write  b.Clock16M


( Core commands that generate BDM traffic. These no longer overlap with the
  chat commands. Start instead with 20 hex - 32 decimal. Anything < 20 will
  exit the BDM loop and return to chat.)

-- : b.Idle ;

( A simple buffer for chatty communication protocols. Words for putting
  values into a buffer -- at pad -- and taking them out again.)

: !chat  <# ;
: #chat  hld @  pad - ;
: +hld   1 hld +! ;

: b>  ( byte)    hld @  c!  +hld ;
: b<  ( - byte)  hld @  c@  +hld ;

( For S08 targets, we send up to four bytes, and expect up to two.)
variable bdm-cmd
: Send1    bdm-cmd !  !chat ;
: Send2    Send1  b> ;
: Send3    Send2  b> ;
: Send4    Send3  b> ;

( How to determine whether a cmd should be a control write, a control read,
  or a write followed by a read.

  If expect = 0, then
    pure control write
  else if #chat = 0
    pure control read
  else
    control write, then
    control read)

( expect generates BDM commands with a repeat of 1. We have to use separate
  words to get repeated reads or writes.)

: expect  ( #expect)
   =if  ( expecting *something*, so read or write-then-read)
      #chat if  ( we're writing too, so write-then-read)
         push ( #expect)
         bdm-cmd @  1  #chat  r@  pad  u.BdmWrite
         pop ( #expect)  u.Result  !chat  ^
      then

      ( control read)
      bdm-cmd @  1  rot ( expect)  pad  u.BdmRead  !chat  ^
   then
   drop  ( 0)

   ( control write)
   bdm-cmd @  1  #chat  0  pad  u.BdmWrite ;

: Expect0   0 expect ;
: Expect1   1 expect  b< ;
: Expect2   2 expect  b< b< ;

: b.Sync    u.Sync  pad bew@ ;
