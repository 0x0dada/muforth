( This file is part of muFORTH: http://muforth.nimblemachines.com/

  Copyright 2002-2012 David Frech. All rights reserved, and all wrongs
  reversed. (See the file COPYRIGHT for details.)

.ifndef >hilo

loading Support for handling 16-bit words

( I'm not sure where to put this. It's not specific to a particular target,
  so having it here in target/common/ makes sense.)

( Generally helpful for little-endian targets.)
: >hilo  ( w - hi lo)  dup 8 u>>  swap "0ff and ;
: lohi>  ( lo hi - w)  8 << or ;

( Little-endian word fetch and store.)
: lew@  ( a - w)              dup c@ ( lo)  swap 1+ c@ ( hi)  lohi> ;
: lew!  ( w a)    push  >hilo  r@ c! ( lo)   pop 1+ c! ( hi) ;

( Generally helpful for big-endian targets.)
: >lohi  ( w - lo hi)  dup "0ff and  swap 8 u>> ;
: hilo>  ( hi lo - w)  swap  8 << or ;

( Big-endian word fetch and store.)
: bew@  ( a - w)              dup c@ ( hi)  swap 1+ c@ ( lo)  hilo> ;
: bew!  ( w a)    push  >lohi  r@ c! ( hi)   pop 1+ c! ( lo) ;

.then
