( meta.mu4 - bits common to all metacompiler targets)

( daf & rst. whatever)
( daf 2001-feb-02. Created, from v25 version.)
( daf 2002-apr-01. Converted to muForth.)

cr s" Meta compiler common"  file[#

forth  decimal

( Metacompiler vocabs)
chain .machine.  ( words specific to a target machine)
chain .outside.  ( used outside of target definitions - the `meta' forth)
chain .inside.   ( used inside target definitions - the `meta' compiler)
chain .asm.      ( the host-resident target assembler)
chain .target.   ( the target words - like `forth' on the host)

: machine   .machine. definitions ;
: outside   .outside. definitions ;
: inside     .inside. definitions ;
: asm           .asm. definitions ;
: target     .target. definitions ;

compiler
: \m   ( compile from machine)  .machine. \chain ;
: \o   ( compile from outside)  .outside. \chain ;
: \i   ( compile from inside)    .inside. \chain ;
: \a   ( compile from asm)          .asm. \chain ;
: \t   ( compile from target)    .target. \chain ;

forth
: \   .outside. chain' execute ;  ( escape to meta - find and execute a word) 

outside
: \     .forth. chain' execute ;  ( escape to forth - find and execute a word)


( Random useful goodies.)
: hex      hex  ;
: decimal  decimal  ;
: octal    octal  ;
: .     .  ;
: u.    u.  ;
: .s    .s  ;

: (     \f (  ;
: char  \f char  ;
: ."    \f ."  ;
: s"    \f s"  ;

: cr    cr  ;
: file[   file[  ;
: ]file   ]file  ;
: ld     ld  ;


#]file
