( This file is part of muFORTH: http://muforth.nimblemachines.com/

  Copyright 2002-2014 David Frech. All rights reserved, and all wrongs
  reversed. (See the file COPYRIGHT for details.)

loading Serial expect/send

( Simple target serial expect/send code.)

: target-raw  ( fd)  ( This is tailored for target interaction)
  dup here get-termios drop   here set-termios-target-raw
  ( fd)  here set-termios  ( set to raw!)  ;

variable fd-target

( tty-target returns fd of tty connected to target. If not yet opened,
  opens it, and sets it to target-raw.)

: tty-target  fd-target @  =if ^ then ( already opened!)  drop
   z" serial-target" open-file-rw  ( this should be a symlink to device)
   dup fd-target !  dup target-raw  ( fd) ;

( Now automagically opens target tty if necessary.)
: bps   ( speed)
   tty-target here get-termios drop ( returns size of struct)
     ( speed) here set-termios-speed
   tty-target here set-termios ;

( Recv from, send to target.)
: _send  tty-target  >emit ;
: _recv  tty-target  <key ;
: flush  tty-target  tty-iflush ;
: drain  tty-target  tty-drain ;
: icount  ( - chars-waiting)  tty-target  tty-icount ;
